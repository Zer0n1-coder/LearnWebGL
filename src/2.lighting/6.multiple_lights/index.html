<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8" />
    <title>LearnWebGL</title>
    <style type="text/css">
        .container {
            margin: 0 auto;
            width: 800px;
        }
    </style>
    <script id="colorsVs" type="x-shader/x-vertex">
        attribute vec3 aPos;
        attribute vec3 aNormal;
        attribute vec2 aTexCoords;

        varying vec3 FragPos;
        varying vec3 Normal;
        varying vec2 TexCoords;

        uniform mat4 changeMat;
        uniform mat4 model;
        uniform mat4 view;
        uniform mat4 projection;

        void main()
        {
        FragPos = vec3(model * vec4( aPos, 1.0));
        Normal = mat3(changeMat)*aNormal;
        TexCoords = aTexCoords;

        gl_Position = projection* view * model * vec4( aPos, 1.0);
        }
    </script>
    <script id="colorsFs" type="x-shader/x-fragment">
        #ifdef GL_ES
        precision mediump float;
        #endif

        struct Material {
        sampler2D diffuse;
        sampler2D specular;
        float shininess;
        };

        struct DirLight {
        vec3 direction;

        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
        };

        struct PointLight {
        vec3 position;

        float constant;
        float linear;
        float quadratic;

        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
        };

        struct SpotLight {
        vec3 position;
        vec3 direction;
        float cutOff;
        float outerCutOff;

        float constant;
        float linear;
        float quadratic;

        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
        };

        #define NR_POINT_LIGHTS 4

        varying vec3 FragPos;
        varying vec3 Normal;
        varying vec2 TexCoords;

        uniform vec3 viewPos;
        uniform DirLight dirLight;
        uniform PointLight pointLights[NR_POINT_LIGHTS];
        uniform SpotLight spotLight;
        uniform Material material;

        vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir);
        vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir);
        vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir);

        void main()
        {
        vec3 norm = normalize(Normal);
        vec3 viewDir = normalize(viewPos - FragPos);

        //平行光
        vec3 result = CalcDirLight(dirLight, norm, viewDir);
        // 点光源
        for(int i = 0; i < NR_POINT_LIGHTS; i++)
        result += CalcPointLight(pointLights[i], norm, FragPos, viewDir);
        // 聚光灯
        result += CalcSpotLight(spotLight, norm, FragPos, viewDir);

        gl_FragColor = vec4(result, 1.0);
        }

        vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir)
        {
        vec3 lightDir = normalize(-light.direction);
        // diffuse shading
        float diff = max(dot(normal, lightDir), 0.0);
        // specular shading
        vec3 reflectDir = reflect(-lightDir, normal);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        // combine results
        vec3 ambient = light.ambient * vec3(texture2D(material.diffuse, TexCoords));
        vec3 diffuse = light.diffuse * diff * vec3(texture2D(material.diffuse, TexCoords));
        vec3 specular = light.specular * spec * vec3(texture2D(material.specular, TexCoords));
        return (ambient + diffuse + specular);
        }

        // 
        vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
        {
        vec3 lightDir = normalize(light.position - fragPos);
        // diffuse shading
        float diff = max(dot(normal, lightDir), 0.0);
        // specular shading
        vec3 reflectDir = reflect(-lightDir, normal);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        // attenuation
        float distance = length(light.position - fragPos);
        float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));
        // combine results
        vec3 ambient = light.ambient * vec3(texture2D(material.diffuse, TexCoords));
        vec3 diffuse = light.diffuse * diff * vec3(texture2D(material.diffuse, TexCoords));
        vec3 specular = light.specular * spec * vec3(texture2D(material.specular, TexCoords));
        ambient *= attenuation;
        diffuse *= attenuation;
        specular *= attenuation;
        return (ambient + diffuse + specular);
        }

        // 
        vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
        {
        vec3 lightDir = normalize(light.position - fragPos);
        // diffuse shading
        float diff = max(dot(normal, lightDir), 0.0);
        // specular shading
        vec3 reflectDir = reflect(-lightDir, normal);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        // attenuation
        float distance = length(light.position - fragPos);
        float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));
        // spotlight intensity
        float theta = dot(lightDir, normalize(-light.direction));
        float epsilon = light.cutOff - light.outerCutOff;
        float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);
        // combine results
        vec3 ambient = light.ambient * vec3(texture2D(material.diffuse, TexCoords));
        vec3 diffuse = light.diffuse * diff * vec3(texture2D(material.diffuse, TexCoords));
        vec3 specular = light.specular * spec * vec3(texture2D(material.specular, TexCoords));
        ambient *= attenuation * intensity;
        diffuse *= attenuation * intensity;
        specular *= attenuation * intensity;
        return (ambient + diffuse + specular);
        }

    </script>
    <script id="lampVs" type="x-shader/x-vertex">
        attribute vec3 aPos;

        uniform mat4 model;
        uniform mat4 view;
        uniform mat4 projection;

        void main()
        {
        gl_Position = projection* view * model * vec4( aPos, 1.0);
        }
    </script>
    <script id="lampFs" type="x-shader/x-fragment">
        #ifdef GL_ES
        precision mediump float;
        #endif

        void main()
        {
        gl_FragColor = vec4(1.0);
        }
    </script>
</head>

<body>
    <img id="container2" src="./container2.png" style="display :none" />
    <img id="container2_specular" src="./container2_specular.png" style="display :none" />
    <img id="matrix" src="./matrix.jpg" style="display :none" />
    <div class="container">
        <canvas id="webgl" width="800" height="600" align="center">
            Please use a browser that supports "canvas"
        </canvas>
    </div>
    <script type="module">
        import { main } from './main.js'
        main();
    </script>
</body>
</html>